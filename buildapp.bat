@echo off
rem
rem $Id: BuildApp.bat $
rem

:BUILDAPP

   cls

   pushd "%~dp0"
   set HG_START_DP_BUILDAPP_BAT=%CD%
   popd

   if /I not "%1" == "/C" goto ROOT
   shift
   set HG_ROOT=
   set HG_HRB=
   set HG_MINGW=
   set HG_BCC=
   set HG_CCOMP=
   set LIB_GUI=
   set LIB_HRB=
   set BIN_HRB=

:ROOT

   if not "%HG_ROOT%" == "" goto TEST
   set HG_ROOT=%HG_START_DP_BUILDAPP_BAT%

:TEST

   set HG_HM30=
   set HG_HM32=
   set HG_HM3264=
   set HG_HM34=
   set HG_HM3464=

   if /I "%1" == "HM30"   goto TEST_HM30
   if /I "%1" == "HM32"   goto TEST_HM32
   if /I "%1" == "HM3264" goto TEST_HM3264
   if /I "%1" == "HM34"   goto TEST_HM34
   if /I "%1" == "HM3464" goto TEST_HM3464

:DETECT_HM30

   if not exist "%HG_ROOT%\BuildApp30.bat"   goto DETECT_HM32
   if     exist "%HG_ROOT%\BuildApp32.bat"   goto SYNTAX
   if     exist "%HG_ROOT%\BuildApp3264.bat" goto SYNTAX
   if     exist "%HG_ROOT%\BuildApp34.bat"   goto SYNTAX
   if     exist "%HG_ROOT%\BuildApp3464.bat" goto SYNTAX
   goto TEST_HM30

:DETECT_HM32

   if not exist "%HG_ROOT%\BuildApp32.bat"   goto DETECT_HM3264
   if     exist "%HG_ROOT%\BuildApp3264.bat" goto SYNTAX
   if     exist "%HG_ROOT%\BuildApp34.bat"   goto SYNTAX
   if     exist "%HG_ROOT%\BuildApp3464.bat" goto SYNTAX
   goto TEST_HM32

:DETECT_HM3264

   if not exist "%HG_ROOT%\BuildApp3264.bat" goto DETECT_HM34
   if     exist "%HG_ROOT%\BuildApp34.bat"   goto SYNTAX
   if     exist "%HG_ROOT%\BuildApp3464.bat" goto SYNTAX
   goto TEST_HM3264

:DETECT_HM34

   if not exist "%HG_ROOT%\BuildApp34.bat"   goto DETECT_HM3464
   if     exist "%HG_ROOT%\BuildApp3464.bat" goto SYNTAX
   goto TEST_HM34

:DETECT_HM3464

   if exist "%HG_ROOT%\BuildApp3464.bat" goto TEST_HM3464
   echo File %HG_ROOT%\BuildApp30.bat not found !!!
   echo File %HG_ROOT%\BuildApp32.bat not found !!!
   echo File %HG_ROOT%\BuildApp3264.bat not found !!!
   echo File %HG_ROOT%\BuildApp34.bat not found !!!
   echo File %HG_ROOT%\BuildApp3464.bat not found !!!
   echo.
   goto END

:SYNTAX

   echo Syntax:
   echo    To build with Harbour 3.0 and MinGW
   echo       BuildApp [/C] HM30 file [options]
   echo    To build with Harbour 3.2 and MinGW
   echo       BuildApp [/C] HM32 file [options]
   echo    To build with Harbour 3.2 and MinGW, 64 bits
   echo       BuildApp [/C] HM3264 file [options]
   echo    To build with Harbour 3.4 and MinGW
   echo       BuildApp [/C] HM34 file [options]
   echo    To build with Harbour 3.4 and MinGW, 64 bits
   echo       BuildApp [/C] HM3464 file [options]
   echo.
   echo    Options:
   echo       f        to redirect all output to error.log
   echo       -rebuild to force compiling of all sources
   echo       -trace   to see the commands generated by hbmk2
   echo       -info    to see informational messages
   echo       -gtwin   to build a console or mixed mode app
   echo       other hbmk2 options
   echo.
   goto END

:TEST_HM30

   if /I "%1" == "HM30" shift
   if exist "%HG_ROOT%\BuildApp30.bat" goto BUILDAPP_HM30
   echo File %HG_ROOT%\BuildApp30.bat not found !!!
   echo.
   goto END

:TEST_HM32

   if /I "%1" == "HM32" shift
   if exist "%HG_ROOT%\BuildApp32.bat" goto BUILDAPP_HM32
   echo File %HG_ROOT%\BuildApp32.bat not found !!!
   echo.
   goto END

:TEST_HM3264

   if /I "%1" == "HM3264" shift
   if exist "%HG_ROOT%\BuildApp3264.bat" goto BUILDAPP_HM3264
   echo File %HG_ROOT%\BuildApp3264.bat not found !!!
   echo.
   goto END

:TEST_HM34

   if /I "%1" == "HM34" shift
   if exist "%HG_ROOT%\BuildApp34.bat" goto BUILDAPP_HM34
   echo File %HG_ROOT%\BuildApp34.bat not found !!!
   echo.
   goto END

:TEST_HM3464

   if /I "%1" == "HM3464" shift
   if exist "%HG_ROOT%\BuildApp3464.bat" goto BUILDAPP_HM3464
   echo File %HG_ROOT%\BuildApp3464.bat not found !!!
   echo.
   goto END

:BUILDAPP_HM30

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb30
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib
   if "%LIB_HRB%"  == "" set LIB_HRB=lib
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   set HG_HM30=yes
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:BUILDAPP_HM32

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb32
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb\mingw
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\mingw
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   set HG_HM32=yes
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:BUILDAPP_HM3264

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb3264
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb\mingw64
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\mingw64
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   set HG_HM3264=yes
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:BUILDAPP_HM34

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb34
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb34\mingw
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\clang
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   set HG_HM34=yes
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:BUILDAPP_HM3464

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb3464
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb34\mingw64
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\clang64
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   set HG_HM3464=yes
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:END

   set HG_START_DP_BUILDAPP_BAT=
