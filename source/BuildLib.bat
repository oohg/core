@echo off
rem
rem $Id: BuildLib.bat $
rem

rem Use: BuildLib [f] [hbmk2 options]
rem f parameter redirectes all output to a file named error.log
rem hbmk2 usefull options:
rem -rebuild to force compiling of all sources
rem -trace to see the commands generated by hbmk2
rem -info to see informational messages

:BUILDLIB

   cls

   pushd "%~dp0"
   set HG_START_DP_BUILDLIB_BAT=%CD%
   popd

   if /I not "%1" == "/C" goto ROOT
   shift
   set HG_ROOT=
   set HG_HRB=
   set HG_MINGW=
   set HG_BCC=
   set HG_CCOMP=
   set LIB_GUI=
   set LIB_HRB=
   set BIN_HRB=

:ROOT

   if not "%HG_ROOT%" == "" goto TEST
   pushd "%HG_START_DP_BUILDLIB_BAT%\.."
   set HG_ROOT=%CD%
   popd

:TEST

   if /I "%1" == "HM30" goto TEST_HM30
   if /I "%1" == "HM32" goto TEST_HM32
   if /I "%1" == "HM34" goto TEST_HM34

:DETECT_HM30

   if not exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib30.bat" goto DETECT_HM32
   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib32.bat" goto SYNTAX
   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib34.bat" goto SYNTAX
   goto BUILDLIB_HM30

:DETECT_HM32

   if not exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib32.bat" goto DETECT_HM34
   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib34.bat" goto SYNTAX
   goto BUILDLIB_HM32

:DETECT_HM34

   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib34.bat" goto BUILDLIB_HM34
   echo File %HG_START_DP_BUILDLIB_BAT%\BuildLib30.bat not found !!!
   echo File %HG_START_DP_BUILDLIB_BAT%\BuildLib32.bat not found !!!
   echo File %HG_START_DP_BUILDLIB_BAT%\BuildLib34.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:SYNTAX

   echo Syntax:
   echo    To build with Harbour 3.0 and MinGW
   echo       BuildLib HM30 [options]
   echo   To build with Harbour 3.2 and MinGW
   echo       BuildLib HM32 [options]
   echo   To build with Harbour 3.4 and MinGW
   echo       BuildLib HM34 [options]
   echo.
   goto END

:TEST_HM30

   shift
   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib30.bat" goto BUILDLIB_HM30
   echo File %HG_START_DP_BUILDLIB_BAT%\BuildLib30.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:TEST_HM32

   shift
   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib32.bat" goto BUILDLIB_HM32
   echo File %HG_START_DP_BUILDLIB_BAT%\BuildLib32.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:TEST_HM34

   shift
   if exist "%HG_START_DP_BUILDLIB_BAT%\BuildLib34.bat" goto BUILDLIB_HM34
   echo File %HG_START_DP_BUILDLIB_BAT%\BuildLib34.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:BUILDLIB_HM30

   if "%HG_HRB%   "== "" set HG_HRB=%HG_ROOT%\hb30
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib
   if "%LIB_HRB%"  == "" set LIB_HRB=lib
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%HG_START_DP_BUILDLIB_BAT%\BuildLib_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:BUILDLIB_HM32

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb32
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb\mingw
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\mingw
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%HG_START_DP_BUILDLIB_BAT%\BuildLib_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:BUILDLIB_HM34

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb34
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb34\mingw
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\clang
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%HG_START_DP_BUILDLIB_BAT%\BuildLib_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:END

   set HG_START_DP_BUILDLIB_BAT=
